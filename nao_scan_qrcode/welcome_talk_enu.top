topic: ~welcome_talk()
language: enu

# Defining all the concepts here
concept:(yes) [yes yeah yeh yep yea yup "{most} definitely" absolutely certainly "{most} certainly" totally "{possibly} maybe" "go [on ahead "for it"]"
              "why not" totally definitely "go ahead" "{yes yep yeah} [I'd "I would"] love [to that]" indeed ofcourse "of course"]
concept:(no) [no nope "I don't think so" "not at all" "not even close {enough}" "not really"]
concept:(okay) [ok alright fine okay]
concept:(great) [great awesome nice fantastic]
concept:(bye) [bye "good bye" "see you" "see ya" "see you {later}" "see you next time" adios]
concept:(next) ["Who is in the queue next?" "Next visitor please" "Who is next?" "Next person please"]
concept:(repeat_it) ["[restate repeat] {me} [the your] {last} question"
                    "what is the question {again}"
                    "repeat ["the sentence" "your last sentence" "what you just said"] {again} {please}"
                    "{"can you"} repeat {please}"
                    "{"can you"} ask {me} the question {again}"
                    "{"can you"} ask {me} the question one more time"
                    "tell ["it to me" me] again"
                    "what [is was] the question {again}"
                    "what ["did you say" "were you [saying "asking me"]"] {again}"
                    "ask {me} [the that] question {again}"
                    "repeat [it please]"
                    "come again"
                    "[say ask] {that} again"
                    "what was the question" what where]
concept:(nao_repeat) ["I was talking about the Museum membership." "Please reply yes or no"]
concept:(not_membership) ["if you would like unlimited exhibition entries by yourself or with loved ones, you can consider getting a membership"
                      "if you would like an unlimited amount of entries to the Gallery, you may want to consider getting a membership"
                      "if you are interested, there is an oppourtunity to become a member where you can enjoy a lot of other benefits through getting a memebrship."]
concept:(member) ["Hi member! It's been some time. Welcome back."
                  "It's nice to see our members again." "Welcome back member! Good to see you again."]

# From the nao_project class check if a person is member or not
u:(no_membership) By the way, ~not_membership . ^gotoReactivate(interested) ^deactivate(old_member)
u:(yes_membership) ^deactivate(interested) ^gotoReactivate(old_member)

proposal: %interested Are you interested in finding out more?
    u1:(~yes) ~great please head over to the reception after our conversation, where one of our lovely
              staff will talk more about the membership with you.
              Before I let you go, ^gotoReactivate(feedback)
    u1:(~no) That is ~okay . ^gotoReactivate(feedback)
    u1:(~repeat_it) ~nao_repeat ^gotoReactivate(interested)

# Welcome people with Gallery membership back
proposal: %old_member ~member ^gotoReactivate(feedback)

# Ask for feedback
proposal: %feedback ^rand["was any of the facts I told any helpful?"
                         "did you like any of the facts I told you about?"
                         "were the things I just talked about close enough to your interests?"
                         "was my talk close to what you were interested in?"]
    u1:(~yes) ~great I cannot tell you how happy I am right now.
              ^gotoReactivate(goodbye)
    u1:(~no) ^rand["oh no" "oh my {god}" "uh oh"]
            I am very sorry to hear that. I will try my best again next time.
            ^gotoReactivate(goodbye)
    u1:(~repeat_it) Please respond yes or no ^gotoReactivate(feedback)

# Stop the dialogue engine
proposal: %goodbye I won't keep you any longer. Welcome to the Gallery and I hope that you enjoy your visit. ~bye
    u1:(~bye) ~next Please press the Start button on the window that has popped up to start scanning.  $onStopped=1
    u1:(["you too" "same to you" "you as well"])  ~next  Please press the Start button on the window that has popped up to start scanning.  $onStopped=1

# If no one responds in 30 seconds, stop the dialogue engine and move to next person
u:(e:Dialog/NotSpeaking30) Are you still there? Please press the Start button $onStopped=1
